FUNCTION_BLOCK speed // Block definition (there may be more than one block per file)

// Define input variables
VAR_INPUT
  carSpeedKph : REAL;
  frontSensorDistance : REAL;
  lateralVelocity : REAL;
  corner : REAL;
END_VAR

// Define output variable
VAR_OUTPUT
  steering : REAL;
  acceleration : REAL;
  brake : REAL;
  scanangle : REAL;
END_VAR

FUZZIFY carSpeedKph
  TERM slow := (0, 1) (50, 1) (70, 0);
  TERM normal := (60, 0) (100, 1) (150, 0);
  TERM fast := (150, 0) (350, 1) (500, 1);
END_FUZZIFY

FUZZIFY frontSensorDistance
  TERM very_near := (0, 1) (10, 1) (15, 0);
  TERM near      := (7, 0) (20, 1) (60, 0);
  TERM normal    := (40, 0) (75, 1) (100, 0);
  TERM far       := (90, 0) (150,1) (200, 0);
  TERM very_far  := (200, 0) (300, 1);
END_FUZZIFY

FUZZIFY lateralVelocity
  TERM stable := trian -3 0 3;
  TERM drifting := (-30, 1) (-5, 0) (5, 0) (30, 1);
END_FUZZIFY

FUZZIFY corner
  TERM sharp_left_corner := (-1, 1) (-0.7, 0);
  TERM left_corner := trape -0.8 -0.6 -0.5 -0.4;
  TERM mild_left_corner := trape -0.6 -0.45 -0.10 -0.05;
  TERM mild_right_corner := trape 0.05 0.10 0.45 0.6;
  TERM right_corner := trape 0.4 0.5 0.6 0.8;
  TERM sharp_right_corner := (0.7, 0) (1, 1);
END_FUZZIFY

DEFUZZIFY steering
  TERM sharp_left := (-1, 1) (-0.8, 0);
  TERM left := trape -0.8 -0.6 -0.4 -0.3;
  TERM mild_left := trape -0.2 -0.17 -0.14 -0.1;
  TERM mild_right := trape 0.1 0.14 0.17 0.2;
  TERM right :=  trape 0.3 0.4 0.6 0.8;
  TERM sharp_right := (0.8, 0) (1, 1);
  METHOD : COG;
  DEFAULT := 0;
END_DEFUZZIFY

DEFUZZIFY acceleration
  TERM slowly := (0,1) (200,1) (400,0);
  TERM normal := (200,0) (800,1) (1400,0);
  TERM fastly := (800,0) (1600,1);
  METHOD : COG;
  DEFAULT := 0;
END_DEFUZZIFY

DEFUZZIFY brake
  TERM slowly := (0,1) (10,0);
  TERM normal := (10,0) (20,1) (30,0);
  TERM fastly := (30,0) (40,1);
  METHOD : COG;
  DEFAULT := 0;
END_DEFUZZIFY


DEFUZZIFY scanangle
  TERM normal := trian 13 30 45;
  TERM wide   := trian 40 45 60;
  METHOD : COG;
  DEFAULT := 30;
END_DEFUZZIFY


// Inference rules
RULEBLOCK steering
  AND : MIN;  // Use 'min' for 'and'
  ACT : MIN;  // Use 'min' activation method
  ACCU : MAX; // Use 'max' accumulation method

  RULE 1 : IF corner IS sharp_left_corner THEN steering IS sharp_left;
  RULE 2 : IF corner IS left_corner THEN steering IS left;
  RULE 3 : IF corner IS mild_left_corner THEN steering IS mild_left;
  RULE 4 : IF corner IS mild_right_corner THEN steering IS mild_right;
  RULE 5 : IF corner IS right_corner THEN steering IS right;
  RULE 6 : IF corner IS sharp_right_corner THEN steering IS sharp_right;
END_RULEBLOCK

RULEBLOCK speedup
  AND : MIN;  // Use 'min' for 'and'
  ACT : MIN;  // Use 'min' activation method
  ACCU : MAX; // Use 'max' accumulation method

  RULE 1 : IF carSpeedKph IS slow THEN acceleration IS fastly;
END_RULEBLOCK

RULEBLOCK brake
  AND : MIN;  // Use 'min' for 'and'
  ACT : MIN;  // Use 'min' activation method
  ACCU : MAX; // Use 'max' accumulation method

  RULE 1 : IF frontSensorDistance IS normal THEN brake IS normal;
  RULE 2 : IF frontSensorDistance IS near THEN brake IS fastly;
END_RULEBLOCK

RULEBLOCK scanangle
  AND : MIN;  // Use 'min' for 'and'
  ACT : MIN;  // Use 'min' activation method
  ACCU : MAX; // Use 'max' accumulation method

  RULE 1: IF frontSensorDistance is near THEN scanangle IS wide;
END_RULEBLOCK


END_FUNCTION_BLOCK
